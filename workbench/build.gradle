plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id "application"
}

group = 'raspberry.on.board'
version = '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public" }
    maven { url "https://www.datanucleus.org/downloads/maven2" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'net.thisptr:jackson-jq:1.0.0-preview.20220705'  // jackson-jq
    testImplementation "junit:junit:$junit_version"
}

// See https://docs.gradle.org/current/userguide/publishing_maven.html
//     https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        maven(MavenPublication) {  // Required by publishToMavenLocal
            groupId = 'raspberry.on.board' // use $project.group
            artifactId = 'workbench'       // use $project.name
            version = '1.0'                // use $project.version

            from components.java
            pom {
                name = "workbench"
                description = "Workbench, notepad, scratchpad, etc."
                url = "http://github.com/OlivierLD/ROB"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "OlivierLD"
                        name = "Olivier Le Diouris"
                        email = "olivier.lediouris@gmail.com"
                    }
                }
            }
        }
    }
}

mainClassName = "utils.SystemUtils"

shadowJar {
    zip64 true
    // to create an executable jar-file. Requires mainClassName.
    mergeServiceFiles()
}

build.doFirst {
    println "+-------------------------------------------------------------"
    println "| >>> Try 'gradlew run' in common-utils (project.name: $project.name) to see some system info."
    println "+-------------------------------------------------------------"
}

task sysInfo(type: JavaExec, dependsOn: build, description: "Display System Info.") {
    mainClass = 'utils.SystemUtils'
//  jvmArgs "-verbose"
//  classpath project.configurations.default
    classpath sourceSets.main.runtimeClasspath
//  classpath sourceSets.main.output.classesDir
    // Use like ../gradlew sysInfo -P--minimal
    if (project.hasProperty("--minimal")) {
        args += ["--minimal"]
    }
    if (project.hasProperty("--no-free-mem")) {
        args += ["--no-free-mem"]
    }
}

sysInfo.doFirst {
    println "+-------------------------------------------------------------"
    println " This task is deprecated. Use gradle run instead."
    println "+-------------------------------------------------------------"
}

run {
    // Use like ../gradlew sysInfo -P--minimal
    if (project.hasProperty("--minimal")) {
        args += ["--minimal"]
    }
    if (project.hasProperty("--no-free-mem")) {
        args += ["--no-free-mem"]
    }
}
